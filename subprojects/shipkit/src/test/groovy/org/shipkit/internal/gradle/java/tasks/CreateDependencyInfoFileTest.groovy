package org.shipkit.internal.gradle.java.tasks

import org.gradle.testfixtures.ProjectBuilder
import testutil.PluginSpecification

class CreateDependencyInfoFileTest extends PluginSpecification {

    CreateDependencyInfoFile createDependencyInfoFile = new CreateDependencyInfoFile()

    def "should create correct output file"() {
        given:

        def child = new ProjectBuilder().withName("child").withParent(project).build()
        child.setVersion("1.2.3")
        child.setGroup("projGroup")
        def sibling = new ProjectBuilder().withName("sibling").withParent(project).build()
        sibling.setVersion("1.2.3")
        sibling.setGroup("projGroup")

        child.repositories.jcenter()
        child.configurations.create("runtime")
        child.dependencies {
            runtime child.project(":sibling")
            runtime "projGroup:sibling:1.1.1"
            runtime "junit:junit:4.10"
            runtime "org.mockito:mockito-core:2.10.0:sources"
            runtime "org.mockito:mockito-core:2.10.0"
        }

        def runtimeConf = child.configurations.getByName("runtime")

        def createDependencyInfoFileTask = project.tasks.create("createTask", CreateDependencyInfoFileTask)

        def output = tmp.newFile("output")
        createDependencyInfoFileTask.outputFile = output
        createDependencyInfoFileTask.configuration = runtimeConf
        createDependencyInfoFileTask.projectGroup = "projGroup"
        createDependencyInfoFileTask.currentProjectVersion = "1.2.3"

        when:
        createDependencyInfoFile.createDependencyInfoFile(createDependencyInfoFileTask)

        then:
        output.text == """{
\t"description":"This file was generated by Shipkit Gradle plugin. See http:\\/\\/shipkit.org\\n\\n",
\t"dependencies":[
\t\t{
\t\t\t"dependencyGroup":"projGroup",
\t\t\t"dependencyName":"sibling",
\t\t\t"dependencyVersion":"1.1.1"
\t\t},
\t\t{
\t\t\t"dependencyGroup":"junit",
\t\t\t"dependencyName":"junit",
\t\t\t"dependencyVersion":"4.10"
\t\t},
\t\t{
\t\t\t"dependencyGroup":"org.mockito",
\t\t\t"artifactType":"jar",
\t\t\t"dependencyName":"mockito-core",
\t\t\t"artifactName":"mockito-core",
\t\t\t"artifactClassifier":"sources",
\t\t\t"artifactExtension":"jar",
\t\t\t"dependencyVersion":"2.10.0"
\t\t},
\t\t{
\t\t\t"dependencyGroup":"org.mockito",
\t\t\t"dependencyName":"mockito-core",
\t\t\t"dependencyVersion":"2.10.0"
\t\t}
\t]
}"""
    }
}
