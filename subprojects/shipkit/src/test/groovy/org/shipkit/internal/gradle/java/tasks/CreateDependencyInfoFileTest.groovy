package org.shipkit.internal.gradle.java.tasks

import org.gradle.testfixtures.ProjectBuilder
import testutil.PluginSpecification

class CreateDependencyInfoFileTest extends PluginSpecification {

    CreateDependencyInfoFile createDependencyInfoFile = new CreateDependencyInfoFile()

    def "should create correct output file"() {
        given:

        def child = new ProjectBuilder().withName("child").withParent(project).build()
        child.group = "projGroup"
        def sibling = new ProjectBuilder().withName("submodule").withParent(project).build()
        sibling.version = "1.2.3"
        sibling.group = "projGroup"

        child.repositories.jcenter()
        child.configurations.create("runtime")
        child.dependencies {
            runtime child.project(":submodule") // current version of submodule -> skip version info
            runtime "projGroup:submodule2:1.0.0" // old version of submodule2 -> keep version, it's not the same as child project's version
            runtime "junit:junit:4.10"
            runtime (group: 'org.mockito', name: 'mockito-core', version: '2.10.0') {
                artifact {
                    name = 'mockito'
                    extension = 'jar'
                    type = 'sources'
                    classifier = 'jar'
                }
                artifact {
                    name = 'mockito'
                    extension = 'jarExt'
                    type = 'jar'
                    classifier = 'jarClass'
                }
            }
        }

        def runtimeConf = child.configurations.getByName("runtime")

        def output = tmp.newFile("output")
        def createDependencyInfoFileTask = project.tasks.create("createTask", CreateDependencyInfoFileTask) {
            outputFile = output
            configuration = runtimeConf
            projectVersion = "1.2.3"
            projectGroup = "projGroup"
        }

        when:
        createDependencyInfoFile.createDependencyInfoFile(createDependencyInfoFileTask)

        then:
        output.text == "# Description\r\n" +
            "This file was generated by Shipkit Gradle plugin. It contains all declared dependencies of the project. See http://shipkit.org.\r\n" +
            "The format of dependencies is: group:name:version\r\n" +
            "Each of the dependencies may contain artifacts (more nested) formatted like: artifactName:classifier:type:extension\r\n\r\n" +
            "# Dependencies\r\n" +
            " - junit:junit:4.10\r\n" +
            " - org.mockito:mockito-core:2.10.0\r\n" +
            "   - mockito:jar:sources:jar\r\n" +
            "   - mockito:jarClass:jar:jarExt\r\n" +
            " - projGroup:submodule\r\n" +
            " - projGroup:submodule2:1.0.0"
    }
}
