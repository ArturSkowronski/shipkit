// First, apply the publishing plugin
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
    }
}

apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'maven'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories { jcenter() }

version = '0.0.5'
group = 'gradle.plugin.org.mockito'

dependencies {
    compile gradleApi()
    //TODO SF use jcabi to edit issues after the release so that they have the milestone attached
    //compile "com.jcabi:jcabi-github:0.17"

    //Using @jar to avoid transitive dependencies (json-simple declares compile dependency on junit!)
    compile "com.googlecode.json-simple:json-simple:1.1.1@jar"

    testCompile("org.spockframework:spock-core:0.7-groovy-2.0") {
        exclude module: "groovy-all"
    }
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile gradleTestKit()
}

pluginBundle {
    website = 'http://mockito.org/'
    vcsUrl = 'https://github.com/mockito/mockito-release-tools'
    description = 'Release tools and Gradle plugins that automate Mockito continuous delivery.'
    tags = ['mockito', 'continuous delivery', 'release']

    plugins {
        releaseNotesPlugin {
            id = 'org.mockito.release-notes'
            displayName = 'Mockito automated release notes generation plugin'
        }
        releaseWorkflowPlugin {
            id = 'org.mockito.release-workflow'
            displayName = 'Mockito release workflow plugin'
        }
    }

    mavenCoordinates {
        groupId = 'gradle.plugin.org.mockito'
        artifactId = "mockito-release-tools"
    }
}

